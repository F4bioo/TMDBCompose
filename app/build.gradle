plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
}
apply from: "$rootDir/plugins/android-build.gradle"

def apiKeyPropertiesFile = rootProject.file("apiKey.properties")
def apiKeyProperties = new Properties()

if (apiKeyPropertiesFile.exists()) {
    apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))
} else {
    println "The apiKey.properties file not found. Creating a new one, please fill out with your own API key."
    // Note: Do not hard-code your API key here. This line is merely for the purpose of creating the apiKey.properties file.
    apiKeyProperties['API_KEY'] = "\"YOUR_DEFAULT_API_KEY_HERE\""
    apiKeyProperties.store(new FileOutputStream(apiKeyPropertiesFile), null)
}

android {
    namespace Config.namespace

    defaultConfig {
        applicationId Config.namespace

        buildConfigField "String", "API_KEY", apiKeyProperties['API_KEY']
        buildConfigField "String", "BASE_URL", '"https://api.themoviedb.org/3/"'
        buildConfigField "String", "BASE_URL_IMAGE", '"https://image.tmdb.org/t/p/w1280"'
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    // Modules
    implementation project(Modules.local)
    implementation project(Modules.remote)
    implementation project(Modules.domain)
    implementation project(Modules.navigation)
    implementation project(Modules.detail)
    implementation project(Modules.favorite)
    implementation project(Modules.popular)
    implementation project(Modules.search)
    implementation project(Modules.arch)
    implementation project(Modules.design)

    // Core Libraries
    implementation "androidx.core:core-splashscreen:1.0.1"

    // Image Loading
    implementation "io.coil-kt:coil-compose:2.2.2"
    implementation "com.github.bumptech.glide:compose:1.0.0-beta01"

    // Logging
    implementation "com.jakewharton.timber:timber:5.0.1"

    // Data Storage
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "com.google.code.gson:gson:2.10"

    // Compose UI
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2"
    implementation "androidx.compose.material:material-icons-extended:1.5.4"
    implementation "com.google.accompanist:accompanist-flowlayout:0.17.0"
    implementation "androidx.navigation:navigation-compose:2.7.4"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:3.2.1"
    implementation "androidx.paging:paging-compose:3.2.1"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"

    // Lifecycle Scopes
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.2"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.2"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2"

    // Database
    implementation "androidx.room:room-ktx:2.6.0"
    implementation "androidx.room:room-runtime:2.6.0"
    kapt "androidx.room:room-compiler:2.6.0"
}
